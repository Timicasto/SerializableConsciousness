buildscript {
    dependencies {
        classpath 'org.ow2.asm:asm:9.0'
        classpath 'org.ow2.asm:asm-commons:9.0'
        classpath 'org.ow2.asm:asm-tree:9.0'
        classpath 'org.ow2.asm:asm-util:9.0'
    }
}

plugins {
    id 'maven-publish'
    id 'forgified-fabric-loom' version '0.6-SNAPSHOT'
}

apply plugin: 'java'

version = "${minecraft_version}-${mod_version}"
group = 'cc.sukazyo.sericons'
archivesBaseName = 'SerializableConsciousness'

loom {
    silentMojangMappingsLicense()
    dataGen {
        mod 'serializableconsciousness'
    }
    addTaskBeforeRun(":${project.name}:classes")
}

repositories {
    mavenCentral()
}

sourceSets {
    main {
        resources {
            srcDirs += 'src/generated/resources'
        }
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings minecraft.officialMojangMappings()
    forge "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

    compileOnly "com.google.code.findbugs:jsr305:3.+"
}

java {
    withSourcesJar()
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"

    // The Minecraft launcher currently installs Java 8 for users, so your mod probably wants to target Java 8 too
    // JDK 9 introduced a new way of specifying this that will make sure no newer classes or methods are used.
    // We'll use that if it's available, but otherwise we'll use the older option.
    def targetVersion = 8
    if (JavaVersion.current().isJava9Compatible()) {
        options.release.set(targetVersion)
    }
}

remapJar {
    dependsOn(jar)
    input.set(jar.archiveFile)
    archiveClassifier.set("universal")
}

processResources {
    inputs.property 'version', mod_version

    filesMatching("META-INF/mods.toml") {
        expand 'version': mod_version
    }
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : "SerializableConsciousness",
                "Specification-Vendor"    : "Timicasto",
                "Specification-Version"   : "1",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.version,
                "Implementation-Vendor"   : "Timicasto",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file:///${project.projectDir}/mcmodsrepo"
        }
    }
}
